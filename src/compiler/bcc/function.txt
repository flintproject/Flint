%struct-type
%language=C++
%define initializer-suffix ,Token::Type::kUnspecified
%define class-name FunctionHash
%compare-strncmp
%readonly-tables
%enum
struct FunctionEntry { const char *name; Token::Type type; };
%%
abs,Token::Type::kAbs
alloc,Token::Type::kAlloc
arccos,Token::Type::kArccos
arccosh,Token::Type::kArccosh
arccot,Token::Type::kArccot
arccoth,Token::Type::kArccoth
arccsc,Token::Type::kArccsc
arccsch,Token::Type::kArccsch
arcsec,Token::Type::kArcsec
arcsech,Token::Type::kArcsech
arcsin,Token::Type::kArcsin
arcsinh,Token::Type::kArcsinh
arctan,Token::Type::kArctan
arctanh,Token::Type::kArctanh
br,Token::Type::kBr
ceiling,Token::Type::kCeiling
cos,Token::Type::kCos
cosh,Token::Type::kCosh
cot,Token::Type::kCot
coth,Token::Type::kCoth
csc,Token::Type::kCsc
csch,Token::Type::kCsch
deref,Token::Type::kDeref
determinant,Token::Type::kDeterminant
divide,Token::Type::kDivide
eq,Token::Type::kEq
eulergamma,Token::Type::kEulergamma
exp,Token::Type::kExp
exponentiale,Token::Type::kExponentiale
factorial,Token::Type::kFactorial
false,Token::Type::kFalse
floor,Token::Type::kFloor
geq,Token::Type::kGeq
gt,Token::Type::kGt
jmp,Token::Type::kJmp
lb,Token::Type::kLb
ld,Token::Type::kLd
leq,Token::Type::kLeq
ln,Token::Type::kLn
load,Token::Type::kLoad
loadi,Token::Type::kLoadi
log,Token::Type::kLog
log10,Token::Type::kLog10
lt,Token::Type::kLt
max,Token::Type::kMax
min,Token::Type::kMin
minus,Token::Type::kMinus
mmul,Token::Type::kMmul
move,Token::Type::kMove
mult,Token::Type::kMult
neq,Token::Type::kNeq
outerproduct,Token::Type::kOuterproduct
pi,Token::Type::kPi
plus,Token::Type::kPlus
power,Token::Type::kPower
refer,Token::Type::kRefer
rem,Token::Type::kRem
ret,Token::Type::kRet
root,Token::Type::kRoot
save,Token::Type::kSave
scalarproduct,Token::Type::kScalarproduct
sec,Token::Type::kSec
sech,Token::Type::kSech
select2,Token::Type::kSelect2
select3,Token::Type::kSelect3
selrow,Token::Type::kSelrow
sin,Token::Type::kSin
sinh,Token::Type::kSinh
store,Token::Type::kStore
tan,Token::Type::kTan
tanh,Token::Type::kTanh
times,Token::Type::kTimes
transpose,Token::Type::kTranspose
true,Token::Type::kTrue
vectorproduct,Token::Type::kVectorproduct
%%
